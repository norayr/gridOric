 3 DIM M%(15,15)
 4 DIM M$(12,12)
 5 DIM N%(15,15)
 9 PRINT CHR$(17)
 10 CLS:PAPER0
 20 INK 3
 30 PRINT@5,10;"1) Create new grid"
 40 PRINT@5,11;"2) Load grid"
 50 PRINT@5,12;"3) Save grid"
 55 PRINT@5,13;"4) Edit grid"
 60 PRINT@5,14;"5) Write secret text"
 70 PRINT@5,15;"6) Read secret text"
 77 PRINT@5,16;"7) Quit"
 79 PRINT@1,21;"author: Norayr Chilingaryan"
 80 GET B$
 84 IF B$="5" THEN GOTO 1500
 85 IF B$="7" THEN PRINT CHR$(17):END
 86 IF B$="4" THEN GOTO 1400
 90 IF B$="1" THEN GOTO 110
 92 IF B$="3" THEN GOTO 1200
 93 IF B$="2" THEN GOTO 1300
 94 IF B$="6" THEN GOTO 3500
 100 GOTO 80
 110 CLS
 120 PRINT "Please, enter grid side length"
 130 INPUT "(6-12)";A
 140 IF A<6 OR A>12 THEN GOTO 130
 150 CLS
 160 LORES0
 175 M%(14,14)=A
 180 FOR I=1 TO A
 190 FOR J=1 TO A
 200 PLOT I,J,19
 210 M%(I,J)=0
 220 NEXT J
 230 NEXT I
 240 PLOT (INT(A/2)+1),0,"+"
 250 PLOT 1,24,"Cursor keys: Move Space: set a hole"
 255 PLOT 3,26,"Q: Main menu"
 260 X=1:Y=1
 270 PLOT X,Y,21
 280 PLOT 1,21,"STATUS: Place a hole somewhere!     "
 290 GET A$
 300 IF A$=CHR$(8) THEN GOTO 370
 310 IF A$=CHR$(9) THEN GOTO 410
 320 IF A$=CHR$(11) THEN GOTO 450
 330 IF A$=CHR$(10) THEN GOTO 490
 340 IF A$=" " THEN GOTO 530
 350 IF A$="Q" THEN GOTO 10
 360 GOTO 290
 370 GOSUB 850:X=X-1
 380 IF X<1 THEN X=1:GOTO290
 390 GOSUB 860
 400 GOTO 270
 410 GOSUB 850:X=X+1
 420 IF X>A THEN X=A: GOTO 290
 430 GOSUB 860
 440 GOTO 270
 450 GOSUB 850:Y=Y-1
 460 IF Y<1 THEN Y=1:GOTO 290
 470 GOSUB 860
 480 GOTO 270
 490 GOSUB 850:Y=Y+1
 500 IF Y>A THEN Y=A: GOTO 290
 510 GOSUB 860
 520 GOTO 270
 530 IF M%(X,Y)=1 THEN GOTO 531 ELSE GOTO 539
 531 M%(X,Y)=0: PLOT X,Y,19
 532 M%(A+1-Y,X)=0: PLOT A+1-Y,X,19
 533 M%(A+1-X,A+1-Y)=0: PLOT A+1-X,A+1-Y,19
 534 M%(Y,A+1-X)=0: PLOT Y,A+1-X,19
 537 GOTO 290
 539 DN=1:PLOT 1,21,"STATUS: thinking...                 "
 540 SX=X:SY=Y:GOSUB 900
 550 SX=A+1-Y:SY=X:GOSUB 900
 560 SX=A+1-X:SY=A+1-Y:GOSUB900
 570 SX=Y:SY=A+1-X:GOSUB 900
 580 SX=X+1:SY=Y:GOSUB 900
 590 SX=X-1: GOSUB900
 600 SX=X: SY=Y-1: GOSUB 900
 610 SY=Y+1:GOSUB 900
 740 IF DN<>1 THEN PLOT 1,21,"STATUS: Can't place  hole here...":PING:GOTO290
 750 PLOT X,Y,16
 760 M%(X,Y)=1
 770 PLOT (A+1-Y),X,20
 780 M%(A+1-Y,X)=2
 790 PLOT (A+1-X),(A+1-Y),20
 800 M%(A+1-X,A+1-Y)=2
 810 PLOT Y,(A+1-X),20
 820 M%(Y,A+1-X)=2
 830 PLOT 1,21,"STATUS: Hole placed successfully    "
 831 E=1
 832 FOR I=1 TO A
 833 FOR J=1 TO A
 834 IF M%(I,J)=0 THEN E=0
 835 NEXTJ
 836 NEXTI
 840 IF E=0 THEN GOTO 290 ELSE GOTO 1000
 850 NX=X:NY=Y:RETURN
 860 IF M%(NX,NY)=0 THEN PLOT NX,NY,19
 870 IF M%(NX,NY)=1 THEN PLOT NX,NY,16
 880 IF M%(NX,NY)=2 THEN PLOT NX,NY,20
 890 RETURN
 900 IF (SX<0) OR (SY<0) THEN RETURN
 910 IF (SX>A) OR (SY>A) THEN RETURN
 920 IF M%(SX,SY)=1 THEN DN=0
 930 RETURN
 1000 PING: PLOT 1,21,"Congratulations! Perfect grid created"
 1005 PLOT 1,24,"   Press any key to return main menu"
 1006 PLOT 1,26,"              "
 1007 GET W$
 1010 GOTO 10
 1200 CLS: PRINT "Enter grid file name to save"
 1210 GOSUB 3300
 1230 STORE M%,W$
 1240 PRINT "Grid '";
 1241 PRINT W$;
 1242 PRINT "' successfully saved"
 1243 GOTO 1005
 1300 CLS
 1310 PRINT "Enter grid file name to load"
 1320 INPUT W$
 1330 RECALL M%,W$
 1335 A=M%(14,14)
 1340 PRINT "Grid '";:PRINTW$;:PRINT"' loaded successfully"
 1350 GOTO 1005
 1400 LORES0
 1410 IF M%(14,14)=0 THEN PRINT "No grid in memory. Load or create one":GOTO1005
 1430 FOR I=1 TO A
 1440 FOR J=1 TO A
 1450 IF M%(I,J)=0 THEN PLOT I,J,19
 1460 IF M%(I,J)=1 THEN PLOT I,J,16
 1470 IF M%(I,J)=2 THEN PLOT I,J,20
 1480 NEXTJ
 1490 NEXTI
 1499 GOTO 240
 1500 CLS:LORES0
 1501 IF M%(14,14)=0 THENPRINT"No grid in memory. Load or create one":GOTO 1005
 1509 PRINT@1,26;"Esc: exit to menu without saving text"
 1510 K=3101
 1530 GOSUB 3000
 1560 C=1
 1565 H=2
 1570 GOSUB 1800
 1573 K=3105
 1574 GOSUB 3000
 1610 H=3
 1620 GOSUB 1800
 1645 K=3100
 1650 GOSUB 3000
 1660 H=4
 1670 GOSUB 1800
 1692 K=3110
 1693 GOSUB 3000
 1695 H=5
 1700 GOSUB 1800
 1720 GOSUB 4000
 1780 PING
 1787 PRINT@1,25;"Press any key to save this encryption"
 1789 GET A$
 1790 GOTO 3200
 1800 REPEAT
 1810 GET A$
 1811 IF A$=CHR$(27) THEN GOTO 10
 1830 IF A$=CHR$(8) OR A$=CHR$(127) THEN GOSUB 2100 ELSE GOSUB 2000
 1850 UNTIL C=H
 1860 RETURN
 2000 J=1:I=0
 2010 I=I+1
 2020 IF N%(I,J)=1 THEN N%(I,J)=0:PLOT I,J,A$:M$(I,J)=A$: RETURN
 2030 IF I>A THEN I=0: J=J+1
 2040 IF J<=A THEN GOTO 2010 ELSE C=C+1:RETURN
 2100 M$(I,J)=""
 2110 PLOT I,J," "
 2130 N%(I,J)=1
 2150 RETURN
 3000 FOR I=1 TO A
 3010 FOR J=1 TO A
 3015 GOSUB  K
 3030 IF M%(I,J)=1 THEN N%(KI,KJ)=1 ELSE N%(KI,KJ)=0
 3040 IF N%(KI,KJ)=1 THEN PLOT KI,KJ,16 ELSE PLOT KI,KJ,19
 3050 NEXTJ
 3060 NEXTI
 3070 RETURN
 3100 KI=A+1-I:KJ=A+1-J:RETURN
 3101 KI=I:KJ=J:RETURN
 3105 KI=A+1-J:KJ=I:RETURN
 3110 KI=J:KJ=A+1-I:RETURN
 3200 CLS:LORES0
 3270 PRINT "Enter crypted file name"
 3280 GOSUB 3300
 3290 STORE M$,W$
 3299 PRINT "File saved successfully":GOTO 1005
 3300 INPUT W$
 3310 PRINT "Press any key to start saving file": GET A$
 3330 RETURN
 3500 CLS:LORES0
 3505 PRINT "Feature not included in this edition"
 3510 PRINT "Download full version from"
 3520 PRINT "www.geocities.com/nemrout"
 3999 GOTO 1005
 4000 FOR I=1 TO A
 4010 FOR J=1 TO A
 4020 PRINT@I,J;M$(I,J)
 4030 NEXTJ
 4050 NEXT I
 4060 RETURN
